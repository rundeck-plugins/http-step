plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.17.2'
    id 'java'
    id 'maven-publish'
}

apply plugin: 'java'

sourceCompatibility = 1.11
ext.rundeckPluginVersion = '1.2'
ext.pluginClassNames='edu.ohio.ais.rundeck.HttpWorkflowStepPlugin,edu.ohio.ais.rundeck.HttpWorkflowNodeStepPlugin'
ext.pluginName = 'Rundeck Http Step'
ext.pluginDescription = 'A workflow plugin that makes HTTP requests'
group = 'com.github.rundeck-plugins'

scmVersion {
    ignoreUncommittedChanges = true
    tag {
        prefix = ''
        versionSeparator = ''
    }
}
project.version = scmVersion.version


repositories {
    mavenLocal()
    mavenCentral()
}

configurations{
    //declare custom pluginLibs configuration to include only libs for this plugin
    pluginLibs

    //declare compile to extend from pluginLibs so it inherits the dependencies
    implementation {
        extendsFrom pluginLibs
    }
}

dependencies {
    implementation 'org.rundeck:rundeck-core:5.2.0-20240410'

    pluginLibs group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.14'
    pluginLibs group: 'com.google.code.gson', name: 'gson', version:'2.10.1'

    testImplementation group: 'com.github.tomakehurst', name: 'wiremock-standalone', version:'2.23.2'
    testImplementation group: 'junit', name: 'junit', version:'4.13.1'

    testImplementation (
            'org.mockito:mockito-all:1.9.5',
            'org.powermock:powermock-module-junit4:1.5',
            'org.powermock:powermock-api-mockito:1.5'
    )
}


// task to copy plugin libs to output/lib dir
task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}


jar {
    from "$buildDir/output"
    manifest {
        def libList = configurations.pluginLibs.collect{'lib/'+it.name}.join(' ')

        attributes 'Rundeck-Plugin-Classnames': pluginClassNames
        attributes 'Rundeck-Plugin-File-Version': version
        attributes 'Rundeck-Plugin-Name': pluginName
        attributes 'Rundeck-Plugin-Description': pluginDescription
        attributes 'Rundeck-Plugin-Rundeck-Compatibility-Version': '3.x'
        attributes 'Rundeck-Plugin-Tags': 'java,WorkflowStep'
        attributes 'Rundeck-Plugin-License': 'Apache 2.0'
        attributes 'Rundeck-Plugin-Source-Link': 'https://github.com/rundeck-plugins/http-notification'
        attributes 'Rundeck-Plugin-Target-Host-Compatibility': 'all'
        attributes 'Rundeck-Plugin-Version': rundeckPluginVersion
        attributes 'Rundeck-Plugin-Archive': 'true'
        attributes 'Rundeck-Plugin-Libs': "${libList}"

    }
    dependsOn(copyToLib)

}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'http-step'
            from components.java
        }
    }
}